Tela de Sele√ß√£o Turbinada ‚Äì Parte 3 ‚Äì Esconder Elementos
Autor: Mauricio Cruz
Data: 28/12/2010 17:11

Essa dica √© mais para aquele Consultor que curte uma
tela bonita
. Eu sei que muita gente acha que o correto √© simplesmente ‚Äúfazer funcionar‚Äù, mas eu sempre penso que ‚Äú
se √© pra fazer, tem que fazer direito
‚Äú!
O c√≥digo abaixo possue a explica√ß√£o de como fazer para
esconder e mostrar objetos numa tela
de sele√ß√£o de acordo com a a√ß√£o do usu√°rio. √â meu, fica muito
style
a tela hehe. Vamos ao c√≥digo:
REPORT  z_mudatela_zombie.

TABLES: mara.

* Vamos controlar a mudan√ßa de tela atrav√©s de dois radiobuttons
*---------------------------------------------------------------
SELECTION-SCREEN BEGIN OF BLOCK bl01 WITH FRAME.

* √â importante colocar o user-command aqui, pois sem ele o report n√£o
* ir√° passar pelo evento AT SELECTION SCREEN OUTPUT quando o user
* clicar em qualquer um dos radio buttons. √â um pseudo-gato ;)
* Tamb√©m tem que deixar o DEFAULT, sen√£o o radio button n√£o chega
* com o X marcado na primeira visualiza√ß√£o da tela! :(
*---------------------------------------------------------------------
PARAMETERS: rb_tela1 RADIOBUTTON GROUP rb01 USER-COMMAND muda_tela
                                                  DEFAULT 'X',
            rb_tela2 RADIOBUTTON GROUP rb01.

SELECTION-SCREEN END OF BLOCK bl01.

* Tela com Select Options.
* O MODIF ID deve ser o MESMO para todos os elementos da tela
*------------------------------------------------------------
SELECTION-SCREEN BEGIN OF BLOCK bl02 WITH FRAME.

SELECT-OPTIONS: s_matnr FOR mara-matnr MODIF ID t1,
                s_mtart FOR mara-mtart MODIF ID t1.

SELECTION-SCREEN END OF BLOCK bl02.

* Tela com Parameters.
* O MODIF ID deve ser o MESMO para todos os elementos da tela
*------------------------------------------------------------
SELECTION-SCREEN BEGIN OF BLOCK bl03 WITH FRAME.

PARAMETERS: p_matnr TYPE mara-matnr MODIF ID t2,
            p_mtart TYPE mara-mtart MODIF ID t2.

SELECTION-SCREEN END OF BLOCK bl03.

* Evento que ir√° controlar a mudan√ßa da tela. Lembre-se que
* √© necess√°rio o USER-COMMAND nos RadioButtons para o Report passar
* por aqui!
*------------------------------------------------------------------
AT SELECTION-SCREEN OUTPUT.

  PERFORM modifica_tela.

*&---------------------------------------------------------------*
*&      Form  MODIFICA_TELA
*&---------------------------------------------------------------*
FORM modifica_tela .

* O MODIF ID deixa o objeto "Modificavel" na tela de sele√ß√£o.
* Vamos usar um Loop at Screen para modific√°-la.
*------------------------------------------------------------
  LOOP AT SCREEN.

*   Se o user quiser telas com parametros
*------------------------------------------------------------------
    IF rb_tela1 = 'X'.

*     Dentro do campo GROUP1 vai ficar o valor do MODIF ID.
*     Da√≠ √© s√≥ tratar os atributos do elemento como necess√°rio

*     Esconde Tela com Parameters
*--------------------------------
      IF screen-group1 = 'T2'.
        screen-invisible = 1.
        screen-input     = 0.
        screen-active    = 0.
        MODIFY SCREEN.
        CONTINUE.
      ENDIF.

*     Mostra tela com Select Options
*-----------------------------------
      IF screen-group1 = 'T1'.
        screen-invisible = 0.
        screen-input     = 1.
        screen-active    = 1.
        MODIFY SCREEN.
        CONTINUE.
      ENDIF.

    ELSE.

*     Esconde tela com Select Options
*-------------------------------------
      IF screen-group1 = 'T1'.
        screen-invisible = 1.
        screen-input     = 0.
        screen-active    = 0.
        MODIFY SCREEN.
        CONTINUE.
      ENDIF.

*     Mostra tela com Parameters
*--------------------------------
      IF screen-group1 = 'T2'.
        screen-invisible = 0.
        screen-input     = 1.
        screen-active    = 1.
        MODIFY SCREEN.
        CONTINUE.
      ENDIF.

    ENDIF.
  ENDLOOP.

ENDFORM.                    " MODIFICA_TELA
Rodando o c√≥digo acima num Report Z local, fica assim:
Tela mostrando os Select Options
Quando o user escolher o radio button de baixo a tela ir√° mudar! Os select options somem e d√£o lugar aos parameters.
Animal e bem simples, n√£o √©?
Agora d√° pra fazer aquele user carrancudo ficar de boa aberta.
Aproveito este post para desejar um √≥timo ano novos a todos que acessam o blog. 2011 promete!
Abra√ßos!



Coment√°rios:
Vin√≠cius Andrade (10/08/2014 12:58): Muito bem constru√≠do. Quero utilizar em minhas video-aulas . Posso?
  Mauricio Cruz (10/08/2014 19:56): Ol√° Vin√≠cius! Pode sim! S√≥ pe√ßo para voc√™ deixar um link no v√≠deo dizendo de onde voc√™ pegou o c√≥digo, beleza? E for√ßa ae nessas v√≠deo-aulas, sei que n√£o √© f√°cil gravar esse tipo de material. Abra√ßos!
    Vin√≠cius Andrade (11/08/2014 20:03): Opa! Obrigado, pode deixar eu coloco o link. No trabalho todos j√° usam o abapzombie como guia!
    Vin√≠cius Andrade (11/08/2014 20:03): Opa! Obrigado, pode deixar eu coloco o link. No trabalho todos j√° usam o abapzombie como guia!
  Mauricio Cruz (10/08/2014 19:56): Ol√° Vin√≠cius! Pode sim! S√≥ pe√ßo para voc√™ deixar um link no v√≠deo dizendo de onde voc√™ pegou o c√≥digo, beleza? E for√ßa ae nessas v√≠deo-aulas, sei que n√£o √© f√°cil gravar esse tipo de material. Abra√ßos!
    Vin√≠cius Andrade (11/08/2014 20:03): Opa! Obrigado, pode deixar eu coloco o link. No trabalho todos j√° usam o abapzombie como guia!
    Vin√≠cius Andrade (11/08/2014 20:03): Opa! Obrigado, pode deixar eu coloco o link. No trabalho todos j√° usam o abapzombie como guia!
renan zenkner roggia (24/07/2012 13:14): Usei o c√≥digo mas em uma demanda que recebi, mas no meu caso um dos campos √© obrigat√≥rio para a sele√ß√£o. Ou seja, n√£o consigo trocar de tela, alguma dica ?
  Mauricio Cruz (24/07/2012 13:16): ‚ÄúSer obrigat√≥rio‚Äù n√£o quer dizer que voc√™ tenha que escrever o OBLIGATORY no par√¢metro da tela de sele√ß√£o. O que o usu√°rio quer √© que aquele campo seja obrigat√≥riamente preechido. Tire o obligatory do par√¢metro, e fa√ßa a valida√ß√£o se o campo est√° preenchido no come√ßo da sua l√≥gica. Resolvido! üôÇ
    renan zenkner roggia (24/07/2012 13:33): Obrigado Mauricio, atualmente o c√≥digo esta com esta logica. Fiquei com receio de n√£o ser o  mais adequado.
    renan zenkner roggia (24/07/2012 13:33): Obrigado Mauricio, atualmente o c√≥digo esta com esta logica. Fiquei com receio de n√£o ser o  mais adequado.
  Mauricio Cruz (24/07/2012 13:16): ‚ÄúSer obrigat√≥rio‚Äù n√£o quer dizer que voc√™ tenha que escrever o OBLIGATORY no par√¢metro da tela de sele√ß√£o. O que o usu√°rio quer √© que aquele campo seja obrigat√≥riamente preechido. Tire o obligatory do par√¢metro, e fa√ßa a valida√ß√£o se o campo est√° preenchido no come√ßo da sua l√≥gica. Resolvido! üôÇ
    renan zenkner roggia (24/07/2012 13:33): Obrigado Mauricio, atualmente o c√≥digo esta com esta logica. Fiquei com receio de n√£o ser o  mais adequado.
    renan zenkner roggia (24/07/2012 13:33): Obrigado Mauricio, atualmente o c√≥digo esta com esta logica. Fiquei com receio de n√£o ser o  mais adequado.
Thiago Losilla (04/10/2011 08:52): Maan! Very Naaaice esse post kra! Acabei de usar esse c√≥dego ! Voces estao mandando mto bem no site kra! Abra√ßao !!  üòõ  üòõ