RTTS, RTTI, RTTC e voc√™: tudo a ver ‚Äì Parte 2
Autor: Mauricio Cruz
Data: 29/04/2014 08:30

E a√≠, como vai aquele desenvolvimento bizarro em que voc√™ precisa identificar qual o tipo de uma determinada vari√°vel em tempo de execu√ß√£o? Hoje vamos explorar alguns exemplos do RTTS para fazer esse
trabalho sujo
.
Lembrando que na
Parte 1
voc√™ entende a teoria por tr√°s do RTTS e aprende um pouco sobre casting e o operador ‚Äú=?‚Äù. Esta √© a Parte 2 e, na Parte 3, iremos analisar como criar vari√°veis em tempo de execu√ß√£o.
Pegue a sua lupa de Sherlock Homes e vem comigo identificar essas vari√°veis safadas.
Analisando o tipo de uma v√°ri√°vel
Muita gente conhece o bom e velho DESCRIBE para tentar descobrir alguma coisa sobre as vari√°veis em tempo de execu√ß√£o. N√≥s mesmo j√° falamos um pouco do
DESCRIBE TABLE
por aqui. Por√©m, se voc√™ ler o HELP do describe, vai ver que a recomenda√ß√£o √© usar o RTTS ao inv√©s do DESCRIBE e varia√ß√µes, por conta da flexibilidade que as classes do RTTS proporcionam.
Como ignorar essas recomenda√ß√£o √© algo
abapossauriano
, vamos fu√ßar um pouco no funcionamento do RTTS para analisar uma vari√°vel. No exemplo abaixo a vari√°vel ‚Äúmaterial‚Äù est√° referenciada ao DDIC. Experimente trocar de ‚Äúmara-matnr‚Äù para outros tipos para ver diferentes resultados:
DATA: material  TYPE mara-matnr,
      texto     TYPE string.

DATA: relative_name TYPE string.

DATA: o_typedescr TYPE REF TO cl_abap_typedescr.

DATA: x031l TYPE TABLE OF x031l,
      x030l TYPE x030l.

* Criando o objeto para an√°lise. Este objeto far√° refer√™ncia ao
* tipo da vari√°vel "material".
o_typedescr = cl_abap_typedescr=>describe_by_data( material ).

* Nome absoluto cont√©m o nome + identificador. No caso, ser√° \TYPE=MATNR
* que indica que este objeto √© referente a um TYPE MATNR(elemento de dados).
* Experimente trocar o tipo do campo para PSTAT, e voc√™ ver√° que o TYPE muda
* para o elemento de dados, que √© o PSTAT_D.
WRITE / o_typedescr->absolute_name.

* Nome utilizado para refer√™ncia no programa.
relative_name = o_typedescr->get_relative_name( ).
WRITE / relative_name.

* M√©todos que s√≥ funcionam se o tipo referenciado veio do DDIC.
* Se voc√™ trocar de "material" para "texto" na hora de chamar o
* m√©todo describe_by_data, ir√° tomar um DUMP na cabe√ßa.
x031l[] = o_typedescr->get_ddic_object( ).
x030l   = o_typedescr->get_ddic_header( ).

* Tipo da Vari√°vel, neste caso C de CHAR
WRITE: / o_typedescr->type_kind.

* Tamanho da vari√°vel, para o elemento de dados MATNR - 18
WRITE: / o_typedescr->length.
Neste caso utilizamos o m√©todo
describe_by_data
, mas voc√™ pode utilizar outros m√©todos para buscar a inst√¢ncia, como o
describe_by_name
. No caso do e
describe_by_name
, voc√™ teria que passar o nome do tipo direto ‚Äì ‚ÄúMATNR‚Äù ( pelo
describe_by_data
vocc√™ passa¬†a vari√°vel).
Para quem n√£o est√° acostumado com OO, este exemplo pode levar a uma pergunta importante‚Ä¶
U√©, mas onde foi parar o CREATE OBJECT?
Quando voc√™ precisa utilizar um objeto, nem sempre isso quer dizer que voc√™ precisar√° cri√°-lo. √â extremamente comum no mundo do ABAP Objects delegar a atividade de cria√ß√£o para algum m√©todo de alguma classe. Tipo aqueles¬†chefes que s√≥ delegam, delegam, delegam e n√£o fazem nada.
S√©rio agora: no caso do exemplo anterior, quem ir√° executar o ‚ÄúCREATE OBJECT‚Äù √© o m√©todo ‚Äú
cl_abap_typedescr=>describe_by_data
‚Äú. Esse m√©todo √© um m√©todo est√°tico, ou seja, n√£o precisa ser acessado a partir de uma inst√¢ncia. Se voc√™ n√£o entendeu nada, imagine que chamar um m√©todo est√°tico p√∫blico de uma classe da SE24, √© igual a chamar uma fun√ß√£o, no sentido de ser um peda√ßo de c√≥digo que est√° em algum lugar e que¬†pode ser acessado por qualquer programa.
Pelo lado do design do RTTS, imagine que voc√™ est√° fazendo a seguinte requisi√ß√£o:
‚Äú
Hey RTTS, me devolva a inst√¢ncia certa, preenchida com todos os dados dessa vari√°vel que estou te enviando
‚Äú
. Saber disso, por hora, √© o suficiente üôÇ .
Para os curiosos de plant√£o, existe uma constru√ß√£o cl√°ssica (design pattern) em OO que se chama
Factory.
Ela abusa de m√©todos est√°ticos para que a classe crie a inst√¢ncia da melhor maneira poss√≠vel. D√™ uma olhada
neste link
para saber como funciona, com exemplos em ABAP. Diz a lenda que eu vou fazer um post disso algum dia, mas √© s√≥ uma lenda urbana e pode ser mentira, tipo bicho pap√£o, saci perer√™, geisy arruda‚Ä¶
Descobrindo os campos de uma estrutura
Agora que voc√™ entendeu como instanciar objetos do RTTS, e como fazer an√°lises em uma vari√°vel simples, est√° na hora de utilizar a maravilinda an√°lise¬†de estruturas.
Manja quando voc√™ tem aquela estrutura maldita e precisa descobrir se ela tem o campo que voc√™ precisa, na posi√ß√£o que voc√™ precisa, do tipo que voc√™ precisa, do elemento de dados que voc√™ precisa, aaaaaaaAAaAaAAa? Pois seus problemas acabaram! üôÑ Descobrir os campos de uma estrutura, sua posi√ß√£o e outras parafern√°lias nunca foi t√£o f√°cil!
DATA: estrutura TYPE mara.

DATA: componentes TYPE cl_abap_structdescr=>component_table.

DATA: structdescr   TYPE REF TO cl_abap_structdescr,
      campodescr    TYPE REF TO cl_abap_datadescr.

FIELD-SYMBOLS <component> LIKE LINE OF cl_abap_structdescr=>components.

structdescr ?= cl_abap_structdescr=>describe_by_data( estrutura ).

* Aqui voc√™ recebe a lista de todos os componentes da estrutura, com outros
* objetos j√° referenciados de acordo com o tipo do campo. (abra no debug para entender)
* Com isso voc√™ pode saber O QUE VOC√ä QUISER sobre QUALQUER CAMPO da estrutura.
* N√£o √© feiti√ßaria, √© SAPLoLogia (s√≥ pra rimar)
componentes = structdescr->get_components( ).

* Vamos analisar um campo espec√≠fico da estrutura:
campodescr = structdescr->get_component_type( 'MATNR' ). "Troque o campo para experimentar!"
WRITE: /01 'MATNR',
       10 '√â do tipo',
       20 campodescr->type_kind.

*Ah, mas tudo que eu quero √© um lista dos campos e j√° era!
LOOP AT structdescr->components ASSIGNING  <component>.
*   T√°, toma a√≠
    WRITE: /01 <component>-name,
            20 <component>-decimals,
            30 <component>-length,
            40 <component>-type_kind,
            50 sy-tabix. "posi√ß√£o na estrutura
ENDLOOP.
Viu ali o operador =? entrando em cena? Isso acontece porque o m√©todo ‚Äú
describe_by_data
‚Äù est√° declarado na classe pai CL_ABAP_TYPEDESCR, e esse m√©todo retorna um par√¢metro do tipo TYPEDESCR. Por√©m, como programadores espertos que somos, sabemos que
AQUELE
‚Äú
describe_by_data‚Äù
da classe CL_ABAP_STRUCTDESCR vai retornar uma inst√¢ncia da classe CL_ABAP_STRUCTDESCR. √â a√≠ que o =? entra em jogo, e dizemos para o verificador de sintaxe n√£o encher o saco porque a gente sabe o que est√° fazendo.
AI MEU DEUS MAURICIO,¬†N√ÉO ENTENDI NADA DESSE √öLTIMO PAR√ÅGRAFO!!!1!!1!!1!!
Leia a
Parte 1
. Se mesmo assim n√£o entendeu, pergunta pra gente a√≠ nos coment√°rios üôÇ .
Com¬†a classe CL_ABAP_STRUCTDESCR voc√™ descobre os campos da estrutura, mas para analisar um campo espec√≠fico, vai ter que usar a classe para o tipo espec√≠fico daquele campo. Debugue o exemplo que voc√™ vai me entender!
Bom, espero que agora a hierarquia de classes esteja fazendo um pouco mais de sentido para voc√™ üòõ
An√°lises em Tabelas
E, por fim, vamos analisar uma tabela interna. Acho que voc√™ j√° sacou que a classe CL_ABAP_TABLEDESCR vai fazer uma an√°lise geral na tabela, mas voc√™ vai precisar utilizar a hierarquia de classes para analisar o campos (CL_ABAP_DATADESCR e derivados) de uma linha espec√≠fica (CL_ABAP_STRUCTDESCR).
DATA: tabela TYPE TABLE OF mara.

DATA: tabledescr  TYPE REF TO cl_abap_tabledescr,
      structdescr TYPE REF TO cl_abap_structdescr.

FIELD-SYMBOLS <key> LIKE LINE OF tabledescr->key.

tabledescr ?= cl_abap_tabledescr=>describe_by_data( tabela ).

* Vamos fu√ßar na linha da tabela
structdescr ?= tabledescr->get_table_line_type( ).
* Note que o programa faz um downcast do tipo que GET_TABLE_LINE_TYPE retorna para o
* objeto STRUCTDESCR.
* E aqui voc√™ pode usar o exemplo de estruturas para fu√ßar no objeto STRUCTDESCR.
* [insira o c√≥digo do exemplo anterior aqui]

* Campos da tabela
LOOP AT tabledescr->key ASSIGNING <key> .
  WRITE / sy-tabix.    "Posi√ß√£o do Campo
  WRITE 20 <key>-name. "N√∫meros da Mega-Sena (quem sabe assim alg√∫em testa este exemplo!)
ENDLOOP.

* Tem mais um monte de atributos que descrevem outras coisas da tabela
* interna. Explore!
√â meu, t√¥ falando‚Ä¶ esse tal RTTS √© pior que paparazzi na praia de copacabana.
Guarde sua lupa porque por hoje chega pessoal. Espero que os exemplos estejam claros o suficiente para que voc√™ possa fazer seus pr√≥prios experimentos com as classes do RTTS. N√£o adianta ficar lendo, tem que executar, testar e estudar!
Abra√ßos a todos aqueles que usam a boina do Sherlock Holmes.



Coment√°rios:
Leandro Tentoni (26/08/2016 17:50): Fala Abaper‚Äôs!! seguinte, utilizei as dicas aqui e tenho outra relacionada: * M√©todos que s√≥ funcionam se o tipo referenciado veio do DDIC. * Se voc√™ trocar de ‚Äúmaterial‚Äù para ‚Äútexto‚Äù na hora de chamar o * m√©todo describe_by_data, ir√° tomar um DUMP na cabe√ßa. Para evitar o Dump √© s√≥ verificar se campo verificado vem do ddic, da seguinte forma: ‚ÄúRecebe o nome do dominio (somente para campos do dicion√°rio) o_typedescr = cl_abap_typedescr=>describe_by_data( material ). ‚ÄúVerifica se tipo √© do dicion√°rio CHECK o_typedescr->is_ddic_type( ) EQ abap_true. √© isso ai, espero ter ajudado! Abra√ßos!
  Leandro Tentoni (26/08/2016 17:53): Desconsiderem essa parte: ‚Äú‚ÄúRecebe o nome do dominio (somente para campos do dicion√°rio)‚Äù  heheheheeh abra√ßos
  Leandro Tentoni (26/08/2016 17:53): Desconsiderem essa parte: ‚Äú‚ÄúRecebe o nome do dominio (somente para campos do dicion√°rio)‚Äù  heheheheeh abra√ßos
Fawcs (29/04/2014 09:10): Esses dias eu estava com pregui√ßa de procurar no google o nome dessas classes quando apareceu esse tweet no meu feed üòâ foi uma m√£o na roda! Eu quera analisar uns dados e como eu sou pregui√ßoso demais pra aprender as funcionalidades todas do excel‚Ä¶ fiz um programa que eu copiava uma planilha e ele gerava um alv pra mim igualzinho üòÄ
  Mauricio Cruz (29/04/2014 09:13): A√≠ sim heim Fawcs! Fico feliz de ter ajudado. RTTS est√° longe de ser algo novo, mas √© bem legal e merece esta sequ√™ncia de posts. Abra√ßos! üôÇ
    Fawcs (29/04/2014 09:15): √â mano, ABAP Zombie superando o Google! Falando em algo novo‚Ä¶ Sabe se existe algum reposit√≥rio ou uma maneira de saber a partir de qual vers√£o uma classe/fun√ß√£o est√° dispon√≠vel no SAP? No momento a maneira que eu tenho de descobrir isso √© logando num ambiente 4.7 e procurando!
      Mauricio Cruz (29/04/2014 09:17): Putz n√£o sei‚Ä¶ deve dar para saber olhando a data de cria√ß√£o do objeto abap. Mas s√≥ pesquisando para saber!
      Mauricio Cruz (29/04/2014 09:17): Putz n√£o sei‚Ä¶ deve dar para saber olhando a data de cria√ß√£o do objeto abap. Mas s√≥ pesquisando para saber!
    Fawcs (29/04/2014 09:15): √â mano, ABAP Zombie superando o Google! Falando em algo novo‚Ä¶ Sabe se existe algum reposit√≥rio ou uma maneira de saber a partir de qual vers√£o uma classe/fun√ß√£o est√° dispon√≠vel no SAP? No momento a maneira que eu tenho de descobrir isso √© logando num ambiente 4.7 e procurando!
      Mauricio Cruz (29/04/2014 09:17): Putz n√£o sei‚Ä¶ deve dar para saber olhando a data de cria√ß√£o do objeto abap. Mas s√≥ pesquisando para saber!
      Mauricio Cruz (29/04/2014 09:17): Putz n√£o sei‚Ä¶ deve dar para saber olhando a data de cria√ß√£o do objeto abap. Mas s√≥ pesquisando para saber!
  Mauricio Cruz (29/04/2014 09:13): A√≠ sim heim Fawcs! Fico feliz de ter ajudado. RTTS est√° longe de ser algo novo, mas √© bem legal e merece esta sequ√™ncia de posts. Abra√ßos! üôÇ
    Fawcs (29/04/2014 09:15): √â mano, ABAP Zombie superando o Google! Falando em algo novo‚Ä¶ Sabe se existe algum reposit√≥rio ou uma maneira de saber a partir de qual vers√£o uma classe/fun√ß√£o est√° dispon√≠vel no SAP? No momento a maneira que eu tenho de descobrir isso √© logando num ambiente 4.7 e procurando!
      Mauricio Cruz (29/04/2014 09:17): Putz n√£o sei‚Ä¶ deve dar para saber olhando a data de cria√ß√£o do objeto abap. Mas s√≥ pesquisando para saber!
      Mauricio Cruz (29/04/2014 09:17): Putz n√£o sei‚Ä¶ deve dar para saber olhando a data de cria√ß√£o do objeto abap. Mas s√≥ pesquisando para saber!
    Fawcs (29/04/2014 09:15): √â mano, ABAP Zombie superando o Google! Falando em algo novo‚Ä¶ Sabe se existe algum reposit√≥rio ou uma maneira de saber a partir de qual vers√£o uma classe/fun√ß√£o est√° dispon√≠vel no SAP? No momento a maneira que eu tenho de descobrir isso √© logando num ambiente 4.7 e procurando!
      Mauricio Cruz (29/04/2014 09:17): Putz n√£o sei‚Ä¶ deve dar para saber olhando a data de cria√ß√£o do objeto abap. Mas s√≥ pesquisando para saber!
      Mauricio Cruz (29/04/2014 09:17): Putz n√£o sei‚Ä¶ deve dar para saber olhando a data de cria√ß√£o do objeto abap. Mas s√≥ pesquisando para saber!